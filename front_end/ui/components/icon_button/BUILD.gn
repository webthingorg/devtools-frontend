# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../../scripts/build/ninja/devtools_module.gni")
import("../visibility.gni")

node_action("generate_css") {
  script = "front_end/ui/components/icon_button/generate-css.js"

  args = [ rebase_path(target_gen_dir, root_build_dir) ]

  outputs = [ "$target_gen_dir/IconButton.css" ]
  data = outputs

  if (is_debug) {
    metadata = {
      grd_files = outputs
    }
  }
}

devtools_module("icon_button") {
  sources = [
    "Icon.ts",
    "IconButton.ts",
  ]
  deps = [
    "../../../ui/components/helpers:bundle",
    "../../../ui/components/render_coordinator:bundle",
    "../../../ui/lit-html:bundle",
  ]

  visibility = [ "../../../../test/unittests/front_end/ui/components" ]
}

devtools_entrypoint("bundle") {
  entrypoint = "icon_button.ts"

  deps = [ ":icon_button" ]

  visibility = [
    # TODO(crbug.com/1202788): Remove invalid dependents
    "../../legacy/components/color_picker/*",
    "../../legacy/components/object_ui/*",
  ]

  visibility += default_components_visibility
}
