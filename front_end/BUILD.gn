# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../scripts/build/ninja/copy.gni")
import("../scripts/build/ninja/vars.gni")
import("../third_party/typescript/typescript.gni")

group("front_end") {
  public_deps = [
    ":devtools_app",
    ":entrypoint_copy",
    ":html_entrypoints",
    ":inspector",
    ":js_app",
    ":legacy_entrypoints",
    ":ndb_app",
    ":node_app",
    ":shell",
    ":toolbox",
    ":ts_library-verification",
    ":worker_app",
    "bindings:bundle",
    "client_variations:bundle",
    "color_picker:bundle",
    "common:bundle",
    "component_helpers:bundle",
    "components:bundle",
    "console_counters:bundle",
    "cookie_table:bundle",
    "data_grid:bundle",
    "devices:bundle",
    "diff:bundle",
    "emulated_devices/optimized:optimized_emulated_devices",
    "event_listeners:bundle",
    "extensions:bundle",
    "formatter:bundle",
    "formatter_worker:worker_entrypoint",
    "har_importer:bundle",
    "heap_snapshot_model:bundle",
    "heap_snapshot_worker:worker_entrypoint",
    "i18n:bundle",
    "inline_editor:bundle",
    "javascript_metadata:bundle",
    "js_main:bundle",
    "layer_viewer:bundle",
    "lighthouse_worker:bundle",
    "main:bundle",
    "object_ui:bundle",
    "perf_ui:bundle",
    "protocol_client:bundle",
    "quick_open:bundle",
    "recorder:bundle",
    "screencast:bundle",
    "sdk:bundle",
    "search:bundle",
    "services:bundle",
    "snippets:bundle",
    "source_frame:bundle",
    "startup:bundle",
    "test_runner:bundle",
    "text_editor:bundle",
    "text_utils:bundle",
    "theme_support:bundle",
    "third_party/lighthouse",
    "third_party/puppeteer:bundle",
    "timeline_model:bundle",
    "toolbox_bootstrap:bundle",
    "ui:bundle",
    "ui/components:bundle",
    "wasmparser_worker:worker_entrypoint",
    "worker_main:bundle",
    "worker_service:bundle",
    "worker_service:bundle",
    "workspace:bundle",
    "workspace_diff:bundle",
  ]

  data_deps = [
    ":embedder-scripts",
    ":html_entrypoints",
    ":legacy_css",
  ]
}

group("legacy_test-resources") {
  public_deps = [
    "accessibility_test_runner:bundle",
    "application_test_runner:bundle",
    "axe_core_test_runner:bundle",
    "bindings_test_runner:bundle",
    "console_test_runner:bundle",
    "coverage_test_runner:bundle",
    "cpu_profiler_test_runner:bundle",
    "data_grid_test_runner:bundle",
    "device_mode_test_runner:bundle",
    "elements_test_runner:bundle",
    "extensions_test_runner:bundle",
    "heap_profiler_test_runner:bundle",
    "layers_test_runner:bundle",
    "lighthouse_test_runner:bundle",
    "network_test_runner:bundle",
    "performance_test_runner:bundle",
    "sdk_test_runner:bundle",
    "security_test_runner:bundle",
    "sources_test_runner:bundle",
  ]
}

group("legacy_css") {
  public_deps = [
    "cm:legacy_css",
    "components:legacy_css",
    "console:legacy_css",
    "console_counters:legacy_css",
    "data_grid:legacy_css",
    "elements:legacy_css",
    "emulation:legacy_css",
    "help:legacy_css",
    "inline_editor:legacy_css",
    "inspector_main:legacy_css",
    "mobile_throttling:legacy_css",
    "object_ui:legacy_css",
    "persistence:legacy_css",
    "text_editor:legacy_css",
    "ui:legacy_css",
  ]
}

group("legacy_entrypoints") {
  public_deps = [
    "accessibility:legacy",
    "animation:legacy",
    "bindings:legacy",
    "browser_debugger:legacy",
    "changes:legacy",
    "color_picker:legacy",
    "common:legacy",
    "components:legacy",
    "console:legacy",
    "console_counters:legacy",
    "cookie_table:legacy",
    "coverage:legacy",
    "data_grid:legacy",
    "developer_resources:legacy",
    "diff:legacy",
    "elements:legacy",
    "emulation:legacy",
    "event_listeners:legacy",
    "extensions:legacy",
    "formatter:legacy",
    "har_importer:legacy",
    "heap_snapshot_model:legacy",
    "heap_snapshot_worker:legacy",
    "help:legacy",
    "host:legacy",
    "inline_editor:legacy",
    "input:legacy",
    "inspector_main:legacy",
    "issues:legacy",
    "javascript_metadata:legacy",
    "js_main:legacy",
    "layer_viewer:legacy",
    "layers:legacy",
    "lighthouse:legacy",
    "linear_memory_inspector:legacy",
    "main:legacy",
    "media:legacy",
    "mobile_throttling:legacy",
    "network:legacy",
    "node_main:legacy",
    "object_ui:legacy",
    "perf_ui:legacy",
    "performance_monitor:legacy",
    "persistence:legacy",
    "profiler:legacy",
    "protocol_client:legacy",
    "protocol_monitor:legacy",
    "quick_open:legacy",
    "resources:legacy",
    "root:legacy",
    "screencast:legacy",
    "sdk:legacy",
    "search:legacy",
    "security:legacy",
    "services:legacy",
    "settings:legacy",
    "snippets:legacy",
    "source_frame:legacy",
    "sources:legacy",
    "text_editor:legacy",
    "text_utils:legacy",
    "timeline:legacy",
    "timeline_model:legacy",
    "ui:legacy",
    "web_audio:legacy",
    "webauthn:legacy",
    "worker_main:legacy",
    "workspace:legacy",
    "workspace_diff:legacy",
  ]
}

ts_library("shell") {
  sources = [ "shell.js" ]

  deps = [
    "bindings:legacy",
    "browser_sdk:bundle",
    "changes:meta",
    "components:legacy",
    "console:meta",
    "console_counters:legacy",
    "coverage:meta",
    "dom_extension:bundle",
    "extensions:legacy",
    "host:legacy",
    "input:meta",
    "linear_memory_inspector:meta",
    "persistence:legacy",
    "persistence:meta",
    "profiler:meta",
    "protocol_client:legacy",
    "protocol_monitor:meta",
    "sdk:legacy",
    "services:legacy",
    "settings:meta",
    "sources:meta",
    "startup:bundle",
    "ui:legacy",
    "workspace:legacy",
  ]
}

ts_library("devtools_app") {
  sources = [ "devtools_app.js" ]

  deps = [
    ":shell",
    "accessibility:meta",
    "animation:meta",
    "browser_debugger:meta",
    "css_overview:meta",
    "developer_resources:meta",
    "elements:meta",
    "emulation:meta",
    "help:meta",
    "inspector_main:meta",
    "issues:meta",
    "layers:meta",
    "lighthouse:meta",
    "media:meta",
    "mobile_throttling:meta",
    "network:meta",
    "performance_monitor:meta",
    "resources:meta",
    "security:meta",
    "startup:bundle",
    "timeline:meta",
    "web_audio:meta",
    "webauthn:meta",
  ]
}

ts_library("inspector") {
  sources = [ "inspector.js" ]

  deps = [
    ":devtools_app",
    "startup:bundle",
  ]
}

ts_library("js_app") {
  sources = [ "js_app.js" ]

  deps = [
    ":shell",
    "js_profiler:meta",
    "startup:bundle",
  ]
}

ts_library("ndb_app") {
  sources = [ "ndb_app.js" ]

  deps = [
    ":shell",
    "startup:bundle",
  ]
}

ts_library("node_app") {
  sources = [ "node_app.js" ]

  deps = [
    ":shell",
    "js_profiler:meta",
    "node_debugger:meta",
    "node_main:meta",
    "startup:bundle",
  ]
}

ts_library("toolbox") {
  sources = [ "toolbox.js" ]

  deps = [ "startup:bundle" ]
}

ts_library("worker_app") {
  sources = [ "worker_app.js" ]

  deps = [
    ":shell",
    "startup:bundle",
  ]
}

copy_sources_to_resources("entrypoint_copy") {
  sources = [ "integration_test_runner.html" ]
}

action("html_entrypoints") {
  inputs = [
    "entrypoint_template.html",
    devtools_location_prepend + "scripts/build/generate_html_entrypoint.js",
  ]

  script = "//third_party/node/node.py"

  args = [
    rebase_path(
        devtools_location_prepend + "scripts/build/generate_html_entrypoint.js",
        root_build_dir),
    "--template",
    rebase_path("entrypoint_template.html", root_build_dir),
    "--out-directory",
    rebase_path(resources_out_dir, root_build_dir),
  ]

  outputs = [
    "$resources_out_dir/devtools_app.html",
    "$resources_out_dir/inspector.html",
    "$resources_out_dir/js_app.html",
    "$resources_out_dir/ndb_app.html",
    "$resources_out_dir/node_app.html",
    "$resources_out_dir/toolbox.html",
    "$resources_out_dir/worker_app.html",
  ]
}

copy_sources_to_resources("embedder-scripts") {
  sources = [
    "Tests.js",
    "devtools_compatibility.js",
  ]
}

# This target is used to perform checks on all generated `.d.ts` files.
# Previously we would only run these checks when `is_debug=false`, but
# this complicated debugging. Therefore, this target now always perform
# the check, but we only check the `.d.ts` files once (rather than for
# every single defined `ts_library`).
#
# Whenever a folder is fully checked by the TypeScript compiler, add
# the folder to the `deps` of this target AND add a corresponding
# `import`-statement to the `verification.ts` file. Without adding
# the `import`-statement, TypeScript will be "smart" and ignore all
# declaration files it doesn't have to use.
ts_library("ts_library-verification") {
  verify_lib_check = true

  sources = [ "verification.ts" ]

  deps = [
    "bindings:bundle",
    "browser_sdk:bundle",
    "color_picker:bundle",
    "common:bundle",
    "dom_extension:bundle",
    "host:bundle",
    "protocol_client:bundle",
    "root:bundle",
    "sdk:bundle",
    "workspace:bundle",
  ]
}
