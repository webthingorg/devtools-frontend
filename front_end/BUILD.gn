# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../scripts/build/ninja/copy.gni")
import("../scripts/build/ninja/devtools_entrypoint.gni")
import("../scripts/build/ninja/node.gni")
import("../scripts/build/ninja/vars.gni")
import("../third_party/typescript/typescript.gni")

group("front_end") {
  public_deps = [
    ":devtools_app",
    ":embedder-scripts",
    ":html_entrypoints",
    ":inspector",
    ":integration_test_runner",
    ":js_app",
    ":legacy_css",
    ":legacy_entrypoints",
    ":ndb_app",
    ":node_app",
    ":shell",
    ":toolbox",
    ":ts_library-verification",
    ":worker_app",
    "bindings:bundle",
    "color_picker:bundle",
    "common:bundle",
    "component_helpers:bundle",
    "components:bundle",
    "cookie_table:bundle",
    "data_grid:bundle",
    "diff:bundle",
    "emulated_devices/optimized:optimized_emulated_devices",
    "extensions:bundle",
    "formatter:bundle",
    "formatter_worker:worker_entrypoint",
    "har_importer:bundle",
    "heap_snapshot_model:bundle",
    "heap_snapshot_worker:worker_entrypoint",
    "i18n:bundle",
    "i18n:i18n_locales",
    "inline_editor:bundle",
    "javascript_metadata:bundle",
    "js_main:bundle",
    "legacy_test_runner",
    "lighthouse_worker:bundle",
    "panels/event_listeners:bundle",
    "protocol_client:bundle",
    "recorder:bundle",
    "render_coordinator:bundle",
    "screencast:bundle",
    "search:bundle",
    "snippets:bundle",
    "startup:bundle",
    "text_editor:bundle",
    "text_utils:bundle",
    "theme_support:bundle",
    "third_party/lighthouse",
    "third_party/puppeteer:bundle",
    "timeline_model:bundle",
    "ui:bundle",
    "ui/components:bundle",
    "wasmparser_worker:worker_entrypoint",
    "worker_main:bundle",
    "workspace:bundle",
    "workspace_diff:bundle",
  ]
}

group("legacy_css") {
  public_deps = [
    "cm:legacy_css",
    "components:legacy_css",
    "data_grid:legacy_css",
    "inline_editor:legacy_css",
    "inspector_main:legacy_css",
    "object_ui:legacy_css",
    "panels/application:legacy_css",
    "panels/console:legacy_css",
    "panels/elements:legacy_css",
    "panels/emulation:legacy_css",
    "panels/help:legacy_css",
    "panels/mobile_throttling:legacy_css",
    "persistence:legacy_css",
    "text_editor:legacy_css",
    "ui:legacy_css",
  ]
}

group("legacy_entrypoints") {
  public_deps = [
    "bindings:legacy",
    "color_picker:legacy",
    "common:legacy",
    "components:legacy",
    "cookie_table:legacy",
    "core/root:legacy",
    "data_grid:legacy",
    "diff:legacy",
    "extensions:legacy",
    "formatter:legacy",
    "har_importer:legacy",
    "heap_snapshot_model:legacy",
    "heap_snapshot_worker:legacy",
    "host:legacy",
    "inline_editor:legacy",
    "javascript_metadata:legacy",
    "linear_memory_inspector:legacy",
    "main:legacy",
    "object_ui:legacy",
    "panels/accessibility:legacy",
    "panels/animation:legacy",
    "panels/application:legacy",
    "panels/browser_debugger:legacy",
    "panels/changes:legacy",
    "panels/console:legacy",
    "panels/console_counters:legacy",
    "panels/coverage:legacy",
    "panels/developer_resources:legacy",
    "panels/elements:legacy",
    "panels/emulation:legacy",
    "panels/event_listeners:legacy",
    "panels/help:legacy",
    "panels/layer_viewer:legacy",
    "panels/layers:legacy",
    "panels/lighthouse:legacy",
    "panels/mobile_throttling:legacy",
    "panels/network:legacy",
    "panels/profiler:legacy",
    "perf_ui:legacy",
    "persistence:legacy",
    "protocol_client:legacy",
    "quick_open:legacy",
    "sdk:legacy",
    "search:legacy",
    "security:legacy",
    "settings:legacy",
    "snippets:legacy",
    "source_frame:legacy",
    "sources:legacy",
    "text_editor:legacy",
    "text_utils:legacy",
    "timeline:legacy",
    "timeline_model:legacy",
    "ui:legacy",
    "web_audio:legacy",
    "workspace:legacy",
    "workspace_diff:legacy",
  ]
}

ts_library("shell") {
  no_emit = true
  sources = [ "shell.js" ]

  deps = [
    "Images:generate_css_vars",
    "bindings:legacy",
    "browser_sdk:meta",
    "components:legacy",
    "core/dom_extension:bundle",
    "extensions:legacy",
    "host:legacy",
    "input:meta",
    "linear_memory_inspector:meta",
    "main:meta",
    "object_ui:meta",
    "panels/changes:meta",
    "panels/console:meta",
    "panels/console_counters:legacy",
    "panels/console_counters:meta",
    "panels/coverage:meta",
    "panels/profiler:meta",
    "panels/protocol_monitor:meta",
    "perf_ui:meta",
    "persistence:legacy",
    "persistence:meta",
    "protocol_client:legacy",
    "quick_open:meta",
    "sdk:legacy",
    "sdk:meta",
    "settings:meta",
    "source_frame:meta",
    "sources:meta",
    "startup:bundle",
    "ui:legacy",
    "workspace:legacy",
  ]
}

ts_library("devtools_app") {
  no_emit = true
  sources = [ "devtools_app.js" ]

  deps = [
    ":shell",
    "inspector_main:meta",
    "issues:meta",
    "panels/accessibility:meta",
    "panels/animation:meta",
    "panels/application:meta",
    "panels/browser_debugger:meta",
    "panels/css_overview:meta",
    "panels/developer_resources:meta",
    "panels/elements:meta",
    "panels/emulation:meta",
    "panels/help:meta",
    "panels/layer_viewer:meta",
    "panels/layers:meta",
    "panels/lighthouse:meta",
    "panels/media:meta",
    "panels/mobile_throttling:meta",
    "panels/network:meta",
    "panels/performance_monitor:meta",
    "security:meta",
    "startup:bundle",
    "timeline:meta",
    "web_audio:meta",
    "webauthn:meta",
  ]
}

ts_library("inspector") {
  no_emit = true
  sources = [ "inspector.js" ]

  deps = [
    ":devtools_app",
    "screencast:meta",
    "startup:bundle",
  ]
}

ts_library("js_app") {
  no_emit = true
  sources = [ "js_app.js" ]

  deps = [
    ":shell",
    "js_profiler:meta",
    "startup:bundle",
  ]
}

ts_library("ndb_app") {
  no_emit = true
  sources = [ "ndb_app.js" ]

  deps = [
    ":shell",
    "startup:bundle",
  ]
}

ts_library("node_app") {
  no_emit = true
  sources = [ "node_app.js" ]

  deps = [
    ":shell",
    "js_profiler:meta",
    "node_debugger:meta",
    "node_main:meta",
    "startup:bundle",
  ]
}

devtools_entrypoint("toolbox") {
  entrypoint = "toolbox.ts"

  deps = [
    "Images:generate_css_vars",
    "core/dom_extension:bundle",
    "core/platform:bundle",
  ]
}

ts_library("worker_app") {
  no_emit = true
  sources = [ "worker_app.js" ]

  deps = [
    ":shell",
    "issues:meta",
    "panels/application:meta",
    "panels/browser_debugger:meta",
    "panels/developer_resources:meta",
    "panels/elements:meta",
    "panels/help:meta",
    "panels/layer_viewer:meta",
    "panels/mobile_throttling:meta",
    "panels/network:meta",
    "startup:bundle",
    "timeline:meta",
  ]
}

group("integration_test_runner") {
  public_deps = [ ":integration_test_runner-gen" ]
  data_deps = public_deps
}

copy_to_gen("integration_test_runner-gen") {
  sources = [ "integration_test_runner.html" ]
}

node_action("html_entrypoints") {
  inputs = [ "entrypoint_template.html" ]

  script = "scripts/build/generate_html_entrypoint.js"

  args = [
    "--template",
    rebase_path("entrypoint_template.html", root_build_dir),
    "--out-directory",
    rebase_path(target_gen_dir, root_build_dir),
  ]

  outputs = [
    "$target_gen_dir/devtools_app.html",
    "$target_gen_dir/inspector.html",
    "$target_gen_dir/js_app.html",
    "$target_gen_dir/ndb_app.html",
    "$target_gen_dir/node_app.html",
    "$target_gen_dir/toolbox.html",
    "$target_gen_dir/worker_app.html",
  ]
}

copy_to_gen("embedder-scripts") {
  sources = [
    "Tests.js",
    "devtools_compatibility.js",
  ]
}

# This target is used to perform checks on all generated `.d.ts` files.
# Previously we would only run these checks when `is_debug=false`, but
# this complicated debugging. Therefore, this target now always perform
# the check, but we only check the `.d.ts` files once (rather than for
# every single defined `ts_library`).
#
# Whenever a folder is fully checked by the TypeScript compiler, add
# the folder to the `deps` of this target AND add a corresponding
# `import`-statement to the `verification.ts` file. Without adding
# the `import`-statement, TypeScript will be "smart" and ignore all
# declaration files it doesn't have to use.
ts_library("ts_library-verification") {
  verify_lib_check = true

  sources = [ "verification.ts" ]

  deps = [
    "bindings:bundle",
    "browser_sdk:bundle",
    "color_picker:bundle",
    "common:bundle",
    "core/dom_extension:bundle",
    "core/root:bundle",
    "host:bundle",
    "protocol_client:bundle",
    "sdk:bundle",
    "workspace:bundle",
  ]
}
