# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../config/gni/all_devtools_files.gni")
import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/devtools_pre_built.gni")
import("../visibility.gni")

devtools_module("issues_manager") {
  sources = [
    "AttributionReportingIssue.ts",
    "ContentSecurityPolicyIssue.ts",
    "ContrastCheckTrigger.ts",
    "CorsIssue.ts",
    "CrossOriginEmbedderPolicyIssue.ts",
    "HeavyAdIssue.ts",
    "Issue.ts",
    "IssuesManager.ts",
    "LowTextContrastIssue.ts",
    "MarkdownIssueDescription.ts",
    "MixedContentIssue.ts",
    "RelatedIssue.ts",
    "SameSiteCookieIssue.ts",
    "SharedArrayBufferIssue.ts",
    "SourceFrameIssuesManager.ts",
    "TrustedWebActivityIssue.ts",
  ]

  deps = [
    "../../core/common:bundle",
    "../../core/i18n:bundle",
    "../../core/sdk:bundle",
    "../../models/bindings:bundle",
    "../../models/workspace:bundle",
    "../../third_party/marked:bundle",
  ]
}

devtools_pre_built("descriptions") {
  sources = []
  data = []
  foreach(file, devtools_issue_description_files) {
    sources += [ "descriptions/$file" ]
    data += [ "$target_gen_dir/descriptions/$file" ]
  }
}

devtools_entrypoint("bundle") {
  entrypoint = "issues_manager.ts"

  deps = [ ":issues_manager" ]

  visibility = [
    ":*",
    "../../../test/unittests/front_end/models/issues_manager/*",
    "../../entrypoints/main/*",
    "../../panels/application/*",
    "../../panels/console/*",
    "../../panels/console_counters/*",
    "../../panels/issues/*",
    "../../panels/network/*",
    "../../panels/sources/*",
    "../../ui/components/docs/issue_counter/*",

    # TODO(crbug.com/1202788): Remove invalid dependents
    "../../../test/unittests/front_end/ui/components/issue_counter/*",
    "../../ui/components/issue_counter/*",
    "../../ui/legacy/components/cookie_table/*",
  ]

  visibility += devtools_models_visibility
}
